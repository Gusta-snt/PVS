binary_tree_nat: THEORY
BEGIN 

  IMPORTING BinaryTreeNat

  %
  % Exercise 1: Write a constant that represents the following binary tree
  %   a
  %  / \
  % b   c
  a,b,c: nat

  ex1: BinaryTreeNat = bin(bin(nil, b, nil), a, bin(nil, c, nil))


  %
  % Exercise 2: Write a function
  %   height: [BinaryTreeNat -> nat]
  % to calculate the heihgt of a binary tree
  %
  % Hint
  % Try a recursive declaration such as
  %   height(t: BinaryTreeNat): RECURSIVE nat =
  %   [...]
  %   MEASURE [...]

  height(tree: BinaryTreeNat): RECURSIVE nat =
    CASES tree OF
      nil: 0,
      bin(l, e, r): max(1+height(l), 1+height(r))
    ENDCASES
    MEASURE tree BY <<


  %
  % Exercise 3: Write a lemma to state that the height of ex1 is 2
  % and prove it.
  %
  % (possible) useful commands:
  % - help <name of command>
  % - expand <name>

  % @QED height_equal_two proved by gusta on Sun, 18 May 2025 21:24:31 GMT
  height_equal_two: LEMMA height(ex1) = 2

  %
  % Exercise 4: Write a function 
  %   size: [BinaryTreeNat -> nat]
  % to return the size of an binary tree
  % i.e., the amount of elements in the tree.
  %

  size(tree: BinaryTreeNat): RECURSIVE nat =
    CASES tree OF
      nil: 0,
      bin(l, e, r): 1 + size(l) + size(r)
    ENDCASES
    MEASURE tree BY <<

  %
  % Exercise 5: Write a lemma to state that the size of ex1 is 3
  % and prove it.
  % 
  % (possible) useful commands:
  % - help <name of command>
  % - expand <name>

  % @QED size_equal_three proved by gusta on Sun, 18 May 2025 21:25:30 GMT
  size_equal_three: LEMMA size(ex1) = 3

  %
  % Exercise 6: Write a function 
  % inorder: [BinaryTreeNat -> list[nat]]
  % that given a BinaryTreeNat, returns a 
  % list containing the in-order traversal of the tree.
  % (Feel free to restate the declaration of inorder above.)
  inorder(tree: BinaryTreeNat): RECURSIVE list[nat] =
    CASES tree OF
      nil: null,
      bin(l, e, r): append(inorder(l), cons(e, inorder(r)))
    ENDCASES
  MEASURE tree BY <<


  %
  % Exercise 7: Prove the following lemma
  % (possible) useful commands:
  % - help <name of command>
  % - expand <name>
  % @QED ex1_inorder proved by gusta on Mon, 19 May 2025 21:14:44 GMT
  ex1_inorder: LEMMA inorder(ex1) = (: b, a, c :)

  %
  %
  %
  %
  %
  %
  %
  %
  %
  %
  %
  %
  %
  % Have you used grind in the proof of the previous lemmas?
  %

  %
  % Exercise 8: Write a lemma to state that the size of a binary tree t
  % is the lenght of inorder(t)
  %
  % (possible) useful lemmas:
  % - BinaryTreeNat_induction
  % - lenght_append[nat], from structures@more_list_props
  IMPORTING structures@more_list_props
  % (possible) useful commands:
  % - help <name of command>
  % - skolem <fnum> (<fresh names>)
  % - use <lemma/axiom name>
  % - replace <fnum (what)> [<fnum (where)>] [:hide? <t|nil>]
  %
  % @QED size_equal_length proved by gusta on Wed, 28 May 2025 01:06:23 GMT
  size_equal_length: LEMMA
    FORALL (tree: BinaryTreeNat): size(tree) = length(inorder(tree))

  % The two following trees are said to be mirror 
  % reflections of each other
  %
  %      c       c
  %     / \     / \
  %    b   e   e   b
  %   / \         / \
  %  a   d       d   a
  %
  % The following predicate states formally when
  % two arbitrary trees are reflections of each other.
  reflection?(t1,t2: BinaryTreeNat): bool =
    inorder(t1) = reverse(inorder(t2))
  %
  %
  % Exercise 9: Write a recursive function that generates the
  % reflection of any binary tree.
  %
  mirror(t: BinaryTreeNat): RECURSIVE BinaryTreeNat =
    CASES t OF
      nil: nil,
      bin(l,e,r): bin(mirror(r),e,mirror(l))
    ENDCASES
  MEASURE t BY <<

  %
  % Exercise 10:Prove that the function is correct, i.e., the
  % result it's the reflection of the input.
  %
  % (possible) useful lemmas:
  % - BinaryTreeNat_induction
  % - reverse_append[nat], from structures@more_list_props
  % - append_append_cons[nat], from structures@more_list_props
  % (possible) useful commands:
  % - help <name of command>
  % - skolem <fnum> (<fresh names>)
  % - flatten
  % - expand <name>
  % - use <lemma/axiom name>
  % - replace <fnum (what)> [<fnum (where)>] [:hide? <t|nil>]

  % @QED mirror_correct proved by gusta on Wed, 28 May 2025 01:11:40 GMT
  mirror_correct: LEMMA
    FORALL(t: BinaryTreeNat): reflection?(t,mirror(t))

END binary_tree_nat